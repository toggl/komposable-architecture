// Top-level build file where you can add configuration options common to all sub-projects/modules.
import com.toggl.komposable.buildsrc.Libs
import com.toggl.komposable.buildsrc.Versions

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Hilt.gradlePlugin
        classpath Libs.Test.junit5Plugin
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.14.2'
    id "com.github.ben-manes.versions" version '0.39.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

apply from: "${rootDir}/scripts/publish-root.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(Versions.ktlint)
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true
            // Set JVM target to 1.8
            jvmTarget = "1.8"
            apiVersion = "1.5"
            languageVersion = "1.5"
            freeCompilerArgs += ["-Xskip-prerelease-check"]
        }
    }
}