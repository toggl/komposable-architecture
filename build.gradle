// Top-level build file where you can add configuration options common to all sub-projects/modules.
import com.toggl.komposable.buildsrc.Libs
import com.toggl.komposable.buildsrc.Versions

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Hilt.gradlePlugin
        classpath Libs.Test.junit5Plugin
        classpath Libs.gradleVersionsPlugin
    }
}

plugins {
    id 'com.diffplug.spotless' version '6.5.2'
    id "com.github.ben-manes.versions" version '0.42.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

ext {
    PUBLISH_DESCRIPTION = 'Kotlin implementation of Point-Free\'s composable architecture'
    PUBLISH_URL = 'https://github.com/toggl/komposable-architecture'
    PUBLISH_LICENSE_NAME = 'Apache License'
    PUBLISH_LICENSE_URL =
            'https://github.com/toggl/komposable-architecture/blob/main/LICENSE'
    PUBLISH_DEVELOPER_ID = 'toggl'
    PUBLISH_DEVELOPER_NAME = 'Toggl Track Android Team ♥️'
    PUBLISH_DEVELOPER_EMAIL = 'support@toggl.com'
    PUBLISH_SCM_CONNECTION =
            'scm:git:github.com/toggl/komposable-architecture.git'
    PUBLISH_SCM_DEVELOPER_CONNECTION =
            'scm:git:ssh://github.com/toggl/komposable-architecture.git'
    PUBLISH_SCM_URL =
            'https://github.com/toggl/komposable-architecture/tree/main'
}

apply from: "${rootDir}/scripts/publish-root.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(Versions.ktlint)
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true
            // Set JVM target to 1.8
            jvmTarget = "1.8"
            apiVersion = "1.5"
            languageVersion = "1.5"
            freeCompilerArgs += ["-Xskip-prerelease-check"]
        }
    }
}